// max30102 libraries
#include <Wire.h>
#include "MAX30105.h"
#include "spo2_algorithm.h"

// oled libraries
#include <SSD1306Ascii.h>
#include <SSD1306AsciiWire.h>

MAX30105 particleSensor;
SSD1306AsciiWire oled;

#define MAX_BRIGHTNESS 255

uint32_t irBuffer[100]; //infrared LED sensor data
uint32_t redBuffer[100];  //red LED sensor data

int32_t bufferLength; //data length
int32_t spo2; //SPO2 value
int8_t validSPO2; //indicator to show if the SPO2 calculation is valid
int32_t heartRate; //heart rate value (still needed for algorithm function)
int8_t validHeartRate; //indicator to show if the heart rate calculation is valid (still needed for algorithm function)

byte readLED = 13; //Blinks with each data read

int prevO2 = 0;

void setup()
{
  Serial.begin(115200); // initialize serial communication at 115200 bits per second:
  Wire.begin();
  
  // set-up for the oled
  oled.begin(&Adafruit128x64, 0x3C);
  oled.setFont(Verdana12);
  oled.clear();
  oled.setCursor(0, 0);

  pinMode(readLED, OUTPUT);
 
  // Initialize sensor
  if (!particleSensor.begin(Wire, I2C_SPEED_FAST)) //Use default I2C port, 400kHz speed
  {
    Serial.println(F("MAX30105 was not found. Please check wiring/power."));
    while (1);
  }
 
  Serial.println(F("Attach sensor to finger with rubber band. Press any key to start conversion"));
  oled.println("Place finger on");
  oled.println("sensor");
  while (Serial.available() == 0) ; //wait until user presses a key
  Serial.read();
  
  byte ledBrightness = 55; //Options: 0=Off to 255=50mA, 60=default, lower if errors occur
  byte sampleAverage = 4; //Options: 1, 2, 4, 8, 16, 32
  byte ledMode = 2; //Options: 1 = Red only, 2 = Red + IR, 3 = Red + IR + Green
  byte sampleRate = 100; //Options: 50, 100, 200, 400, 800, 1000, 1600, 3200
  int pulseWidth = 411; //Options: 69, 118, 215, 411
  int adcRange = 4096; //Options: 2048, 4096, 8192, 16384
  
  particleSensor.setup(ledBrightness, sampleAverage, ledMode, sampleRate, pulseWidth, adcRange); //Configure sensor with these settings
}

void loop()
{
  bufferLength = 100; //buffer length of 100 stores 4 seconds of samples running at 25sps

  //read the first 100 samples, and determine the signal range
  for (byte i = 0 ; i < bufferLength ; i++)
  {
    while (particleSensor.available() == false) //do we have new data?
      particleSensor.check(); //Check the sensor for new data
      
    redBuffer[i] = particleSensor.getRed();
    irBuffer[i] = particleSensor.getIR();
    particleSensor.nextSample(); //We're finished with this sample so move to next sample

    Serial.print(F("red="));
    Serial.print(redBuffer[i], DEC);
    Serial.print(F(", ir="));
    Serial.println(irBuffer[i], DEC);
  }

  //calculate SpO2 after first 100 samples (first 4 seconds of samples)
  maxim_heart_rate_and_oxygen_saturation(irBuffer, bufferLength, redBuffer, &spo2, &validSPO2, &heartRate, &validHeartRate);

  //Continuously taking samples from MAX30102. SpO2 is calculated every 1 second
  while (1)
  {
    //dumping the first 25 sets of samples in the memory and shift the last 75 sets of samples to the top
    for (byte i = 25; i < 100; i++)
    {
      redBuffer[i - 25] = redBuffer[i];
      irBuffer[i - 25] = irBuffer[i];
    }

    //take 25 sets of samples before calculating the SpO2.
    for (byte i = 75; i < 100; i++)
    {
      while (particleSensor.available() == false) //do we have new data?
        particleSensor.check(); //Check the sensor for new data

      digitalWrite(readLED, !digitalRead(readLED)); //Blink onboard LED with every data read

      redBuffer[i] = particleSensor.getRed();
      irBuffer[i] = particleSensor.getIR();
      particleSensor.nextSample(); //We're finished with this sample so move to next sample
     
      //send samples and calculation result to terminal program through UART
     
      Serial.print(F("red="));
      Serial.print(redBuffer[i], DEC);
      Serial.print(F(", ir="));
      Serial.print(irBuffer[i], DEC);
     
      Serial.print(F(", SPO2Valid="));
      Serial.println(validSPO2, DEC);
      
      if (validSPO2 == 1 && prevO2 != spo2) {
          oled.clear();
   
          oled.setCursor(0, 0);
          oled.print("SPO2: ");
          oled.print(spo2, DEC);
          oled.println("%");
      }

      prevO2 = spo2;
    }

    maxim_heart_rate_and_oxygen_saturation(irBuffer, bufferLength, redBuffer, &spo2, &validSPO2, &heartRate, &validHeartRate);
  }
}